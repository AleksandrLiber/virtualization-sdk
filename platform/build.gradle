/*
 * Copyright (c) 2019 by Delphix. All rights reserved.
 */

plugins {
  id "java"
  id "com.google.protobuf"
  id "delphix.python"
}

repositories {
  mavenCentral()
}

dependencies {
  // Necessary to compile generated java protocol buffer libraries.
  compile project(":common")

  // Necessary to compile generated python protocol buffer libraries.
  python project(path: ":common", configuration: "python")
}

protobuf {

  protoc {
    artifact = "com.google.protobuf:protoc:3.6.1"
  }

  // This activates other protoc language targets.
  // https://github.com/google/protobuf-gradle-plugin#default-outputs
  generateProtoTasks {
    all().each { task ->
      task.builtins {
        python {
        }
      }
    }
  }
  generatedFilesBaseDir = "$projectDir/src"
}

artifacts {
  python sdist.distFile
}

dlpxPython {
  sources {
    delphix {
      url = "https://artifactory.delphix.com/artifactory/api/pypi/delphix-virtual-pypi/simple/"
    }
  }

  dist {
    name = "dvp-platform"
  }

  packages {
    protobuf {
      version = "==3.6.1"
    }
    enum34 {
      markers = "python_version < '3.4'"
    }

    "dvp-common" {
      version = "== $project.version"
      path = file(tasks.getByPath(":common:sdist").getDistFile().toString())
    }
  }

  devPackages {
    mock {
      version = ">=2.0"
    }
  }

  supportedPythons {
    "python2.7" {}
  }
}

/*
 * This is a temporary task. 'src/main/java' only contains compiled protobuf classes. Sometimes these get out of date
 * and they need to be cleaned up manually. In the long term, everything should probably be built under 'build' so
 * the 'clean' task automatically deletes them. In the short term though, this task cleans them up.
 */
task removeProtobufJava(type: Delete) {
  delete "${projectDir}/src/main/java"
}

/*
 * As part of running a packages 'setup.py' file, setuptools creates 'egg-info' directories that contain information
 * about the build distribution. These can sometimes cause issues. We should probably build Python distributions in
 * 'build' so these would be created there, however they still could be created in the 'src' directory if someone runs
 * 'setup.py' manually. This is often done during development to install the package for testing.
 */
task removeEggInfo(type: Delete) {
  delete "${projectDir}/src/main/python/dvp_platform.egg-info"
}

clean.dependsOn('removeProtobufJava')
clean.dependsOn('removeEggInfo')

// sdist and the python tests depend on the generated python libraries
// from the protoc plugin. Must manually specify as plugins are not aware
// of each other.
project.afterEvaluate {
  tasks["sdist"].dependsOn tasks["generateProto"]
  tasks["test_python2.7"].dependsOn tasks["generateProto"]
}
